import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        // replace with local version to support newer Gradle
		classpath 'com.radcortez.gradle:openjpa-gradle-plugin:3.2.0'
        //classpath files('lib/openjpa-gradle-plugin-3.1.1-SNAPSHOT.jar')
        classpath 'org.apache.openjpa:openjpa:3.2.2'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'openjpa'

sourceCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
}

configurations.configureEach {
	all*.exclude group: 'org.apache.hadoop'		// not found on Maven Central?
}

// this is the version of the jar-files of Apache POI that are used for testing
def poiVersion = '5.2.5-SNAPSHOT'

// this is the version that is used in the database for this run
// make sure to update classes POIStatus and Report to handle this version correctly
def poiVersionRC = '-5.2.4-RC1'

//provide defaults so we do not need to specify them always
if (!project.hasProperty('resultFile')) {
    ext.resultFile = 'result-' + poiVersion + poiVersionRC + '-' + getDateTime() + '.json'
}
if (!project.hasProperty('poiDir')) {
    ext.poiDir = Os.isFamily(Os.FAMILY_WINDOWS) ? 'C:/workspaces/poi' : '/opt/poi'
}
if (!project.hasProperty('esHost')) {
    ext.esHost = 'localhost'
}
if (!project.hasProperty('esUser')) {
    ext.esUser = ''
}
if (!project.hasProperty('esPassword')) {
    ext.esPassword = ''
}

sourceSets {
	poiIntegrationTest {
		java {
			srcDir 'src/poiIntegTest/java'
		}
		compileClasspath += sourceSets.main.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath
	}
    // we should move this to a separate source-set, but I could not find out easily
	// how to define the dependencies on the poiIntegrationTest source-set then
	poiIntegrationTest {
		java {
			srcDir 'src/testpoi/java'
		}
		resources {
			srcDir 'src/testpoi/resources'
		}
		compileClasspath += sourceSets.main.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath
	}
}

configurations {
	poiIntegrationImplementation.extendsFrom testImplementation
	poiIntegrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.dstadler:commons-dost:1.2.0.3'
    implementation 'org.dstadler:commoncrawldownload:1.0.0.7'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.15.2'
    implementation 'commons-io:commons-io:2.15.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.apache.velocity:velocity:1.7'

    implementation 'org.apache.openjpa:openjpa:3.2.2'
    runtimeOnly 'org.apache.commons:commons-dbcp2:2.10.0'
	runtimeOnly 'org.apache.commons:commons-pool2:2.12.0'
    implementation 'org.apache.derby:derbynet:10.14.2.0'
    runtimeOnly 'org.apache.derby:derbyclient:10.14.2.0'

	// use the desired version of POI

	// Testing with released version of POI
	/*compile 'org.apache.poi:poi:' + poiVersion
    implementation 'org.apache.poi:poi-ooxml:' + poiVersion
    implementation 'org.apache.poi:poi-scratchpad:' + poiVersion
    implementation 'org.apache.xmlgraphics:batik-all:1.12'*/

	// Testing with downloaded pre-release of POI
	/*compile files(poiDir + '/ooxml-lib/xmlbeans-3.0.2.jar')
	//compile files('/opt/xmlbeans/build/ar/xbean.jar')
    implementation files(poiDir + '/compile-lib/batik-all-1.12.jar')
    implementation files(poiDir + '/lib/commons-collections4-4.2.jar')
    implementation files(poiDir + '/lib/commons-compress-1.23.jar')
    implementation files('/tmp/dist.apache.org/repos/dist/dev/poi/' + poiVersion + poiVersionRC + '/maven/poi/poi-' + poiVersion + '.jar')
    implementation files('/tmp/dist.apache.org/repos/dist/dev/poi/' + poiVersion + poiVersionRC + '/maven/poi-ooxml/poi-ooxml-' + poiVersion + '.jar')
    implementation files('/tmp/dist.apache.org/repos/dist/dev/poi/' + poiVersion + poiVersionRC + '/maven/poi-ooxml-schemas/poi-ooxml-schemas-' + poiVersion + '.jar')
    implementation files('/tmp/dist.apache.org/repos/dist/dev/poi/' + poiVersion + poiVersionRC + '/maven/poi-scratchpad/poi-scratchpad-' + poiVersion + '.jar')*/

	// Testing with latest trunk-POI
    implementation files(poiDir + '/lib/main/commons-collections4-4.4.jar')
    implementation files(poiDir + '/lib/main/commons-math3-3.6.1.jar')
    implementation files(poiDir + '/lib/main/SparseBitSet-1.3.jar')
    implementation files(poiDir + '/lib/main/log4j-api-2.19.0.jar')
    implementation files(poiDir + '/lib/main-tests/log4j-core-2.19.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-jupiter-api-5.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-jupiter-engine-5.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-jupiter-params-5.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-platform-commons-1.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-platform-engine-1.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/junit-platform-launcher-1.9.0.jar')
    implementation files(poiDir + '/lib/main-tests/opentest4j-1.2.0.jar')
    implementation files(poiDir + '/lib/ooxml/commons-compress-1.23.jar')
    implementation files(poiDir + '/lib/ooxml/xmlbeans-5.1.1.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-anim-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-awt-util-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-bridge-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-codec-1.15.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-constants-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-css-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-dom-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-ext-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-gvt-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-i18n-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-parser-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-script-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-shared-resources-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-svg-dom-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-svggen-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-svgrasterizer-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-transcoder-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-util-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/batik-xml-1.14.jar')
    implementation files(poiDir + '/lib/ooxml-batik/xml-apis-ext-1.3.04.jar')
    implementation files(poiDir + '/lib/ooxml-batik/xmlgraphics-commons-2.4.jar')
    implementation files(poiDir + '/build/dist/maven/poi/poi-' + poiVersion + '.jar')
    implementation files(poiDir + '/build/dist/maven/poi-ooxml/poi-ooxml-' + poiVersion + '.jar')
    //compile files(poiDir + '/build/dist/maven/poi-ooxml-lite/poi-ooxml-lite-' + poiVersion + '.jar')
    implementation files(poiDir + '/build/dist/maven/poi-ooxml-full/poi-ooxml-full-' + poiVersion + '.jar')
    implementation files(poiDir + '/build/dist/maven/poi-scratchpad/poi-scratchpad-' + poiVersion + '.jar')

	// duplicate a few dependencies to not fail when the POI dependencies are updated
	implementation 'org.apache.commons:commons-compress:1.24.0'
	implementation 'org.junit.jupiter:junit-jupiter:5.10.0'
	implementation 'com.zaxxer:SparseBitSet:1.3'
	implementation 'org.apache.xmlbeans:xmlbeans:5.1.1'
	implementation 'org.apache.logging.log4j:log4j-api:2.21.1'
	implementation 'org.apache.logging.log4j:log4j-core:2.21.1'


	testImplementation 'org.junit.vintage:junit-vintage-engine:5.10.0'
    testImplementation 'org.apache.ant:ant:1.10.14'
    testImplementation 'org.dstadler:commons-test:1.0.0.20'

	// we use the integration-test framework from POI here as well
	poiIntegrationTestImplementation files(poiDir + '/build/dist/maven/poi-integration-tests/poi-integration-' + poiVersion + '-tests.jar')
	// required for NullPrintStream for now
	poiIntegrationTestImplementation files(poiDir + '/build/dist/maven/poi-tests/poi-' + poiVersion + '-tests.jar')
	// dependencies for the Integration-Test part
	poiIntegrationTestImplementation 'org.junit.vintage:junit-vintage-engine:5.10.0'
	poiIntegrationTestImplementation 'org.apache.ant:ant:1.10.14'
}

openjpa {
	includes = ['**/jpa/**']
	excludes = [
		'**/Base.*',
		'**/FileStatus.*'
	]
}

wrapper {
    gradleVersion = '7.6.1'
}

test {
	systemProperties = System.properties

	// show standard out and standard error of the test JVM(s) on the console
	//testLogging.showStandardStreams = true

	// http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
	systemProperties['user.dir'] = workingDir
}

jacoco {
	toolVersion = '0.8.11'
}

tasks.register('poiIntegrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.poiIntegrationTest.output.classesDirs
	classpath = sourceSets.poiIntegrationTest.runtimeClasspath
	shouldRunAfter test
}

tasks.register('processFiles', JavaExec) {
	dependsOn compileJava
	description = 'Iterates over all files in the download-dir and calls the POI integration test on them, recording failures in a json-file'

	jvmArgs = [
			// using this much memory may require increasing limits in /etc/security/limits.conf
			'-Xmx10000m',
			'-Xms10000m',
			// https://docs.oracle.com/javase/8/embedded/develop-apps-platforms/codecache.htm
			//'-XX:+PrintCompilation',
			'-XX:+PrintCodeCache',
			'-XX:ReservedCodeCacheSize=100m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown',
			// to avoid having strange stacktraces with only the Exception-class
			'-XX:-OmitStackTraceInFastThrow',
			// required for JDK 9 to have stuff in javax.xml.bind available
			/*'-Dsun.reflect.debugModuleAccessChecks=true',
            '--add-opens=java.base/java.net=ALL-UNNAMED',
            '--add-opens=java.base/java.io=ALL-UNNAMED',
            '-Djava.locale.providers=JRE,CLDR',*/
			// See note on https://poi.apache.org/components/slideshow/ppt-wmf-emf-renderer.html
			// under "Instructions to run"
			'-Dsun.java2d.renderer=sun.java2d.marlin.MarlinRenderingEngine',

			// see XMLBEANS-525:
			// '-Dxbean.disable.locking=true',

			// https://logging.apache.org/log4j/2.x/manual/configuration.html#AutomaticConfiguration
			'-Dlog4j2.configurationFile=src/main/resources/log4j2.xml',

			// https://stackoverflow.com/a/3753034/411846
			'-Dlog4j.debug'
	]
	args = [resultFile]

	mainClass = 'org.dstadler.commoncrawl.ProcessFiles'
	classpath = sourceSets.poiIntegrationTest.runtimeClasspath
}

tasks.register('processFilesFromJSON', JavaExec) {
	dependsOn compileJava
	description = 'Iterates over all files in the given JSON-file and calls the POI integration test on them, recording failures in a json-file'

	jvmArgs = [
			'-Xmx3g',
			// to avoid having strange stacktraces with only the Exception-class
			'-XX:-OmitStackTraceInFastThrow',
			'-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=/tmp'
	]
	args = [Os.isFamily(Os.FAMILY_WINDOWS) ? 'C:/workspaces/file-type-detection/filetypes.txt' : '/opt/file-type-detection/filetypes.txt']

	mainClass = 'org.dstadler.commoncrawl.ProcessJSONListOfFiles'
	classpath = sourceSets.poiIntegrationTest.runtimeClasspath
}

tasks.register('processResults', JavaExec) {
	dependsOn compileJava
	description = 'Updates the database with the latest results that are stored in a result-file'

	jvmArgs = [
			'-Xmx512m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown'
	]
	args = [resultFile]

	mainClass = 'org.dstadler.commoncrawl.ProcessResults'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.register('report', JavaExec) {
	dependsOn compileJava
	description = 'Generates the report by reading from the database'

	jvmArgs = [
			'-Xmx512m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown'
	]

	mainClass = 'org.dstadler.commoncrawl.Report'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.register('syncReport', Exec) {
	description = 'Publish resulting HTML files on the Apache server'

	commandLine './syncReport.sh'
}

tasks.register('deduplicate', JavaExec) {
	dependsOn compileJava
	description = 'Compare available files in ../download and move duplicate ones to ../backup to not re-process the same content over and over again'

	jvmArgs = [
			'-Xmx1024m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown'
	]

	mainClass = 'org.dstadler.commoncrawl.Deduplicate'
	classpath = sourceSets.poiIntegrationTest.runtimeClasspath
}

tasks.register('exportToElasticsearchFromJSON', JavaExec) {
	dependsOn compileJava
	description = 'Uses a JSON export of the Database with POIStatus objects to write the data to Elasticsearch. ' +
			'See ElasticsearchWriterFromJSON.java for details. ' +
			'Call with ./gradlew -PesHost=host -PesUser=user -PesPassword=pwd exportToElasticsearchFromJSON'

	jvmArgs = [
			'-Xmx2048m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown',
			'-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=/tmp'
	]

	mainClass = 'org.dstadler.commoncrawl.utils.ElasticsearchWriterFromJSON'
	args = [esHost, esUser, esPassword]
	classpath = sourceSets.main.runtimeClasspath
}

tasks.register('processResultsToCSV', JavaExec) {
	dependsOn compileJava
	description = 'Reads the result-file and convers the JSON format to a CSV file'

	jvmArgs = [
			'-Xmx2048m',
			'-javaagent:lib/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=file-leak-detector.exclude,dumpatshutdown',
			'-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=/tmp'
	]

	mainClass = 'org.dstadler.commoncrawl.utils.ProcessResultsToCSV'
	args = [resultFile]
	classpath = sourceSets.main.runtimeClasspath
}

static def getDateTime() {
	new Date().format("yyyy-MM-dd-HH-mm")
}

test.dependsOn enhance
check.dependsOn poiIntegrationTest

tasks.register('checkPOIJars') {
	doLast() {
		System.out.println("Looking for Apache POI binaries in directory " + poiDir)
		assert file(poiDir + '/build/dist/maven/').exists()
		assert file(poiDir + '/build/dist/maven/poi/poi-' + poiVersion + '.jar').exists()
		assert file(poiDir + '/build/dist/maven/poi-ooxml/poi-ooxml-' + poiVersion + '.jar').exists()
		assert file(poiDir + '/build/dist/maven/poi-ooxml-full/poi-ooxml-full-' + poiVersion + '.jar').exists()
		assert file(poiDir + '/build/dist/maven/poi-scratchpad/poi-scratchpad-' + poiVersion + '.jar').exists()
		assert file(poiDir + '/build/dist/maven/poi-integration-tests/poi-integration-' + poiVersion + '-tests.jar').exists()
	}
}
compileJava.dependsOn checkPOIJars
